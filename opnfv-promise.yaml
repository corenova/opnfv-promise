name: opnfv-promise
description: Resource Management for Virtualized Infrastructure
author: Peter K. Lee <peter@intercloud.net>
license: Apache-2.0
homepage: http://wiki.opnfv.org/promise
repository: git://github.com/opnfv/promise.git
keywords:
  - opnfv
  - promise
  - vim
  - nfvi
  - infrastructure
  - openstack
  - nbi
  - yangforge
  - resource
  - reservation
  - capacity
  - allocation

schema: !yang opnfv-promise.yang

# below config provides default parameters
config: !json opnfv-promise.json

dependencies:
  nfv-infrastructure: !yang nfv-infrastructure.yang
  nfv-mano:           !yang nfv-mano.yang

# MODULE model active bindings
module:
  opnfv-promise:
    # rebind to be a computed property
    promise.capacity.total: !coffee/function |
      (prev) -> @computed (->
        combine = (a, b) ->
          for k, v of b.capacity when v?
            a[k] ?= 0
            a[k] += v
          return a
        (@parent.get 'pools')
        .filter (entry) -> entry.active is true
        .reduce combine, {}
      ), type: prev
    # rebind to be a computed property
    promise.capacity.reserved: !coffee/function |
      (prev) -> @computed (->
        combine = (a, b) ->
          for k, v of b.capacity when v?
            a[k] ?= 0
            a[k] += v
          return a
        (@parent.get 'reservations')
        .filter (entry) -> entry.active is true
        .reduce combine, {}
      ), type: prev
    # rebind to be a computed property
    promise.capacity.available: !coffee/function |
      (prev) -> @computed (-> 
        total = @get 'total'
        reserved = @get 'reserved'
        usage = @get 'usage'
        for k, v of total when v?
          total[k] -= reserved[k] if reserved[k]?
          total[k] -= usage[k] if usage[k]?
        total
      ), type: prev

# COMPLEX-TYPE model active bindings (controller logic)
complex-type:
  ResourceElement:
    #properties
    id: !coffee/function |
      (prev) -> prev.set 'default', -> @uuid()

  ResourcePool:
    # properties
    start: !coffee/function |
      (prev) -> prev.set 'default', -> (new Date).toJSON()

    active: !coffee/function |
      (prev) -> @computed (->
        now = new Date
        start = new Date (@get 'start')
        end = switch
          when (@get 'end')? then new Date (@get 'end')
          else now
        (@get 'enabled') and (start <= now <= end)
      ), type: prev

  ResourceReservation:
    # private properties (NOT in YANG schema)
    max-duration: !coffee/function |
      () -> @computed (->
        # hackish... yes
        (@seek name: 'opnfv-promise').get 'promise.policy.reservation.max-duration'
      ), type: 'number', private: true

    # properties
    start: !coffee/function |
      (prev) -> prev.set 'default', -> (new Date).toJSON()

    end: !coffee/function |
      (prev) -> prev.set 'default', ->
        end = (new Date @get 'start')
        max = @get 'max-duration'
        end.setTime (end.getTime() + (max*60*60*1000))
        end.toJSON()

    active: !coffee/function |
      (prev) -> @computed (->
        now = new Date
        start = new Date (@get 'start')
        end   = new Date (@get 'end')
        (@get 'enabled') and (start <= now <= end)
      ), type: prev

    # methods
    validate: !coffee/function |
      (prev) -> (value={}, resolve, reject) ->
        # validate that request contains sufficient data
        for k, v of value.capacity when v? and !!v
          hasCapacity = true
        if (not hasCapacity) and value.resources.length is 0
          return reject "unable to validate reservation record without anything being reserved"
        # time range verifications
        now = new Date
        start = (new Date value.start) if value.start?
        end   = (new Date value.end) if value.end?
        # if start? and start < now
        #   return reject "requested start time #{value.start} cannot be in the past"
        if end? and end < now
          return reject "requested end time #{value.end} cannot be in the past"
        if start? and end? and start > end
          retun reject "requested start time must be earlier than end time"
        resolve this
    save: !coffee/function |
      (prev) -> (resolve, reject) ->
        @invoke 'validate', @get()
        .then (res) ->
          # should do something about this reservation record...
          now = (new Date).toJSON()
          unless (res.get 'created-on')?
            res.set 'created-on', now 
          res.set 'modified-on', now
          resolve res
        .catch (e) -> reject e

# RPC definitions (INTENT interfaces)
rpc:
  create-reservation: !coffee/function |
    (input, output, done) ->
      # 1. create the reservation record
      reservation = @create 'ResourceReservation', input.get()
      requested = reservation.get 'capacity'
      for k, v of requested
        delete requested[k] unless v? and !!v

      reservations = @access 'promise.reservations'

      # 2. verify that requested capacity is available
      @invoke 'query-capacity',
        start: reservation.get 'start'
        end:   reservation.get 'end'
        capacity: 'available'
      .then (res) ->
        unless (res.get 'collections').length > 0
          output.set 'result', 'error'
          output.set 'message', 'no resource capacity available during requested start/end time'
          return done()

        entries = res.get 'utilization'
        start = new Date reservation.get 'start'
        end   = new Date reservation.get 'end'
        for x in [0..entries.length-1]
          t1 = new Date entries[x].timestamp
          break unless t1 < end

          if x < entries.length-1
            t2 = new Date entries[x+1].timestamp
            continue unless t2 > start

          available = entries[x].capacity
          for k, v of requested
            unless available[k] >= v
              output.set 'result', 'conflict'
              output.set 'message', "requested #{k}=#{v} exceeds available #{available[k]} between #{t1} and #{t2}"
              return done()

        # 3. verify that the requested resource identifiers are available
        # TODO...

        # 4. save the reservation record
        reservation.save()
        .then ->
          reservations.push reservation
          output.set 'result', 'ok'
          output.set 'message', 'request accepted'
          output.set 'reservation-id', reservation.id
          done()
        .catch (e) ->
          output.set 'result', 'error'
          output.set 'message', e
          done()

      .catch (err) ->
        output.set 'result', 'error'
        output.set 'message', e
        done()

  query-reservation: !coffee/function |
    (input, output, done) ->
      query = input.get()
      query.capacity = 'reserved'
      @invoke 'query-capacity', query
      .then (res) ->
        output.set 'reservations', res.get 'collections'
        output.set 'utilization', res.get 'utilization'
        done()
      .catch (e) -> done e

  update-reservation: !coffee/function |
    (input, output, done) ->
      reservation = @find 'ResourceReservation', input.get 'reservation-id'
      requested = input.get 'capacity'
      reserved  = reservation.get 'capacity'
      available = @get 'promise.capacity.available'
      for k, v of requested when v?
        unless requested[k] <= (available[k] + reserved[k])
          output.set 'result', 'conflict'
          output.set 'message', "requested #{k}=#{v} exceeds #{available[k] + reserved[k]}"
          return done()

      reservation.invoke 'validate', input.get()
      .then (res) ->
        res.set input.get()
        res.save()
        .then ->
          output.set 'result', 'ok'
          output.set 'message', 'update accepted'
          done()
        .catch (e) ->
          output.set 'result', 'error'
          output.set 'message', e
          done()

  cancel-reservation: !coffee/function |
    (input, output, done) ->
      reservation = @find 'ResourceReservation', input.get 'reservation-id'
      reservation.destroy()
      .then =>
        (@access 'promise.reservations').remove reservation.id
        output.set 'result', 'ok'
        output.set 'message', 'reservation canceled'
        done()
      .catch (e) ->
        output.set 'result', 'error'
        output.set 'message', e
        done()

  query-capacity: !coffee/function |
    (input, output, done) ->
      # 1. we gather up all collections that match the specified window
      window = input.get 'window'

      collections = switch (input.get 'capacity')
        when 'total'     then [ 'ResourcePool' ]
        when 'reserved'  then [ 'ResourceReservation' ]
        when 'used'      then [ 'ResourceAllocation' ]
        when 'available' then [ 'ResourcePool', 'ResourceReservation', 'ResourceAllocation' ]

      matches = collections.reduce ((a, name) =>
        res = @find name,
          start: (value) -> (not window.end?)   or (new Date value) <= (new Date window.end)
          end:   (value) -> (not window.start?) or (new Date value) >= (new Date window.start)
        a.concat res...
      ), []

      if window.scope is 'exclusive'
        # yes, we CAN query filter in one shot above but this makes logic cleaner...
        matches = matches.where
          start: (value) -> (not window.start?) or (new Date value) >= (new Date window.start)
          end:   (value) -> (not window.end?) or (new Date value) <= (new Date window.end)

      output.set 'collections', matches
      unless (input.get 'show-utilization') is true
        return done()

      # 2. we calculate the deltas based on start/end times of each match item
      deltas = matches.reduce ((a, entry) ->
        b = entry.get()
        b.end ?= 'infiniteT'
        [ skey, ekey ] = [ (b.start.split 'T')[0], (b.end.split 'T')[0] ]
        a[skey] ?= count: 0, capacity: {}
        a[ekey] ?= count: 0, capacity: {}
        a[skey].count += 1
        a[ekey].count -= 1
        for k, v of b.capacity when v?
          a[skey].capacity[k] ?= 0
          a[ekey].capacity[k] ?= 0
          if entry.name is 'ResourcePool'
            a[skey].capacity[k] += v
            a[ekey].capacity[k] -= v
          else
            a[skey].capacity[k] -= v
            a[ekey].capacity[k] += v
        return a
      ), {}

      # 3. we then sort the timestamps and aggregate the deltas
      last = count: 0, capacity: {}
      usages = for timestamp in Object.keys(deltas).sort() when timestamp isnt 'infinite'
        entry = deltas[timestamp]
        entry.timestamp = (new Date timestamp).toJSON()
        entry.count += last.count
        for k, v of entry.capacity
          entry.capacity[k] += (last.capacity[k] ? 0)
        last = entry
        entry

      output.set 'utilization', usages
      done()

  register-pool: !coffee/function |
    (input, output, done) ->
      

  # TEMPORARY
  add-provider: !coffee/function |
    (input, output, done) ->
      @using 'mano', ->
        @invoke 'add-provider', (input.get 'endpoint', 'region', 'username', 'password')
        .then (res) =>
          (@access 'promise.providers').push res
          output.set 'result', 'ok'
          output.set 'provider-id', res.id
          done()
